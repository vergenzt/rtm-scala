plugins {
  id "com.github.maiflai.scalatest" version "0.6-5-g9065d91"
}

group = "com.github.vergenzt"
version = "0.1.4"

subprojects {
  apply plugin: "scala"
  apply plugin: "eclipse"

  repositories {
    mavenCentral()
  }

  configurations {
    scalaCompilerPlugin
  }

  dependencies {
    compile "org.scala-lang:scala-library:2.11.7"
    compile "org.scala-lang:scala-compiler:2.11.7"

    compile "org.scala-lang.modules:scala-xml_2.11:1.0.3"
    compile "org.scalaj:scalaj-http_2.11:1.1.4"
    compile "com.github.nscala-time:nscala-time_2.11:1.8.0"

    scalaCompilerPlugin "org.scalamacros:paradise_2.11.7:2.1.0"

    testCompile "org.scalatest:scalatest_2.11:2.2.4"
    testCompile "org.mockito:mockito-core:1.10.19"
    testCompile "com.squareup.okhttp:mockwebserver:2.3.0"

    // for scalatest gradle plugin
    testRuntime "org.pegdown:pegdown:1.1.0"
  }

  tasks.withType(ScalaCompile) {
    scalaCompileOptions.additionalParameters = [
      "-Xplugin:" + configurations.scalaCompilerPlugin.asPath,
      "-Ymacro-debug-lite"
    ]
    options.compilerArgs = ["-Xdebug", "-agentlib:jdwp=transport=dt_socket,server=n,suspend=y,address=9999"]
  }
}

project(":rtm-scala") {
  apply from: "$rootProject.rootDir/gradle/publishing.gradle"

  dependencies {
    compile project(":rtm-scala-lib")
    compile project(":rtm-scala-meta")
  }
}

project(":rtm-scala-meta") {
  dependencies {
    compile project(":rtm-scala-lib")
  }
}

