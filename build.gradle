plugins {
  id "com.github.maiflai.scalatest" version "0.6-5-g9065d91"
}

apply plugin: "scala"
apply plugin: "eclipse"
apply plugin: "maven"
apply plugin: "signing"

apply from: "gradle/credentials.gradle"

group = "com.github.vergenzt"
version = "0.1.4"

repositories {
  mavenCentral()
}

dependencies {
  compile "org.scala-lang:scala-library:2.11.5"

  compile "org.scala-lang.modules:scala-xml_2.11:1.0.3"
  compile "org.scalaj:scalaj-http_2.11:1.1.4"
  compile "com.github.nscala-time:nscala-time_2.11:1.8.0"

  testCompile "org.scalatest:scalatest_2.11:2.2.4"
  testCompile "org.mockito:mockito-core:1.10.19"
  testCompile "com.squareup.okhttp:mockwebserver:2.3.0"

  // for scalatest gradle plugin
  testRuntime "org.pegdown:pegdown:1.1.0"
}

task sourcesJar(type: Jar) {
  classifier = "sources"
  from sourceSets.main.allSource
}

task scaladocJar(type: Jar, dependsOn: scaladoc) {
  classifier = "javadoc"
  from scaladoc.destinationDir
}

artifacts {
  archives jar
  archives sourcesJar
  archives scaladocJar
}

signing {
  sign configurations.archives
}

uploadArchives {
  repositories.mavenDeployer {
    beforeDeployment { deployment -> signing.signPom(deployment) }

    repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
      authentication(userName: "", password: "") // set in credentials.gradle
    }

    pom.project {
      name "rtm-scala_2.11"
      packaging "jar"
      description "rtm-scala is a Scala wrapper for the Remember the Milk API."
      url "https://github.com/vergenzt/rtm-scala"

      scm {
        url "scm:git@github.com:vergenzt/rtm-scala.git"
        connection "scm:git@github.com:vergenzt/rtm-scala.git"
        developerConnection "scm:git@github.com:vergenzt/rtm-scala.git"
      }

      licenses {
        license {
          name "MIT License"
          url "http://opensource.org/licenses/MIT"
          distribution "repo"
        }
      }

      developers {
        developer {
          id "vergenzt"
          name "Tim Vergenz"
          email "vergenzt@gmail.com"
          url "https://github.com/vergenzt"
        }
      }
    }
  }
}

